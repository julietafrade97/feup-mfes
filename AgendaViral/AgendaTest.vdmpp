class AgendaTest is subclass of Tests

	instance variables
		agenda: Agenda := new Agenda();
		userAdmin: Admin := new Admin("julieta@gmail.com", "julieta12345");
		user1: Regular := new Regular("sofia@gmail.com", "sofia12345", "Sofia", "Silva", 500, 30);
		user2: Regular := new Regular("bibi@gmail.com", "bibi12345", "Beatriz", "Baldaia", 200, 5);
		user3: Regular := new Regular("carlos@gmail.com", "carlos12345", "Carlos", "Freitas", 800, 10);
		user4: Regular := new Regular("vicente@gmail.com", "vicente12345", "Vicente", "Espinha", 100, 50);
		event1: Event := new Event("Twenty One Pilots", "Concertos", mk_Event`Date(17,3,2019), mk_Event`Date(17,3,2019), "Twenty One Pilots, o aclamado duo norte-americano constituido por Tyler Joseph e Josh Dun. The Bandito Tour sera a digressÃ£o mundial de apresentacao do album, com a estreia da banda ao vivo em Portugal, dia 17 de Marco, na Altice Arena.", 42, "Lisboa", 20000);
		event2: Event := new Event("EXO", "Concertos", mk_Event`Date(20,5,2019), mk_Event`Date(20,5,2019), "EXO e um grupo masculino sino-coreano de Seul. Estreia em Portugal dia 20 de Maio, na Altice Arena.", 30, "Lisboa", 20000);
		event3: Event := new Event("Aberturas: Tom Emerson em conversa com o arquivo Alvaro Siza", "Exposicoes", mk_Event`Date(6,1,2019), mk_Event`Date(6,2,2019), "Visita orientada a exposicao por Matilde Seabra, educadora. Localizacao: Biblioteca de Serralves", 2.5, "Porto", 200);
		event4: Event := new Event("Brunch Mercearia Bio", "Gastronomia", mk_Event`Date(5,1,2019), mk_Event`Date(5,1,2019), "Ir as compras e aproveitar para tomar um pequeno-almoco reforcado ou antecipar a hora do almoco e a proposta do nosso Brunch, servido entre as 11h e as 16h.", 7.8, "Cascais", 30);
		event5: Event := new Event("Porto VS Belenenses", "Desporto", mk_Event`Date(30,1,2019), mk_Event`Date(30,1,2019), "Lorem ipsum.", 35, "Porto", 50000);
		event6: Event := new Event("Leixoes VS Famalicao", "Desporto", mk_Event`Date(7,4,2020), mk_Event`Date(7,4,2020), "Lorem ipsum.", 12.5, "Matosinhos", 2);
	
		proposed1: Event := new Event("Workshop Comida Saudavel daTerra", "Gastronomia", mk_Event`Date(15,7,2019), mk_Event`Date(15,7,2019), "Workshop de comida saudavel, daTerra baixa, 15h.", 5, "Porto", 20);
		proposed2: Event := new Event("Cozinhar Nunca Foi Facil", "Gastronomia", mk_Event`Date(20,12,2019), mk_Event`Date(20,12,2019), "Lorem.", 10, "Lisboa", 35);
		
	operations
	
		-- Creates agenda and verifies the parameters
		private testCreateAgenda: () ==> ()
		testCreateAgenda() == (
			dcl a: Agenda := new Agenda();
			assertTrue(a.categories = {"Concertos", "Exposicoes", "Gastronomia", "Moda", "Desporto", "Natureza"});
			assertTrue(a.locations = {"Porto" |-> {"Porto", "Matosinhos", "Maia", "Vila Nova de Gaia"}, "Lisboa" |-> {"Lisboa", "Amadora", "Cascais", "Sintra"}, "Faro" |-> {"Faro", "Albufeira", "Portimao"}});
			assertTrue(a.loggedInUser = nil);
		);
		
		private testAddUser: () ==> ()
		testAddUser() == (
			agenda.addUser(userAdmin);
			agenda.addUser(user1);
			agenda.addUser(user2);
			agenda.addUser(user3);
			agenda.addUser(user4);
			assertTrue(agenda.users <> {|->});
		);
		
		private testLoginAdmin: () ==> ()
		testLoginAdmin() == (
			dcl outcome: bool;
			agenda.loggedInUser := nil;
			-- fail
		  outcome:= agenda.login("julieta@gmail.com", "person12345");
		  assertTrue(outcome = false);
			assertTrue(agenda.loggedInUser = nil);
			
			-- success
			outcome := agenda.login("julieta@gmail.com", "julieta12345");
		  assertTrue(outcome = true);
			assertTrue(agenda.loggedInUser <> nil);
			assertTrue(agenda.loggedInUser = userAdmin);
		);
		
		private testAddEvent: () ==> ()
		testAddEvent() == (
			agenda.addEvent(event1);
			agenda.addEvent(event2);
			agenda.addEvent(event3);
			agenda.addEvent(event4);
			agenda.addEvent(event5);
			agenda.addEvent(event6);
			assertTrue(agenda.events <> {});
		);
		
		private testFindByCity: () ==> ()
		testFindByCity() == (
			dcl cityEvents : set of Event := {};
			--fail
			cityEvents := agenda.findByCity("Faro");
			assertTrue(cityEvents = {});
			 
			--success
			cityEvents := agenda.findByCity("Porto");
			assertTrue(cityEvents <>{});
		);
		
		private testFindByDistrict: () ==> ()
		testFindByDistrict() == (
			dcl districtEvents : set of Event := {};
			--fail
			districtEvents := agenda.findByDistrict("Faro");
			assertTrue(districtEvents = {});
			
			districtEvents := agenda.findByDistrict("Braganca");
			assertTrue(districtEvents = {});
			 
			--success
			districtEvents := agenda.findByDistrict("Lisboa");
			assertTrue(districtEvents <> {});
		);
		
		private testFindByCategory: () ==> ()
		testFindByCategory() == (
			dcl categoryEvents : set of Event := {};
			--fail
			categoryEvents := agenda.findByCategory("Moda");
			assertTrue(categoryEvents = {});
			 
			--success
			categoryEvents := agenda.findByCategory("Desporto");
			assertTrue(categoryEvents <>{});
		);
		 
		private testFindByDate: () ==> ()
		testFindByDate() == (
			dcl dateEvents : set of Event := {};
			--fail
			dateEvents := agenda.findByDate(mk_Event`Date(31,1,2018));
			assertTrue(dateEvents = {});
			 
			--success
			dateEvents := agenda.findByDate(mk_Event`Date(5,1,2019));
			assertTrue(dateEvents <>{});
			
		);

		private testFindEvents: () ==> ()
		testFindEvents() == (
			dcl eventsFound : set of Event := {};
			--fail
			eventsFound := agenda.findEvents ("Matosinhos", "", "Moda", mk_Event`Date(7,4,2020));
			assertTrue(eventsFound = {});			
			
			--success
			eventsFound := agenda.findEvents("Matosinhos", "", "", nil);
			assertTrue(eventsFound <>{});
			
			eventsFound := agenda.findEvents("", "Porto", "", nil);
			assertTrue(eventsFound <>{});
			
			eventsFound := agenda.findEvents("", "", "Desporto", nil);
			assertTrue(eventsFound <>{});
			
			eventsFound := agenda.findEvents("", "", "", mk_Event`Date(7,4,2020));
			assertTrue(eventsFound <>{});
			
			eventsFound := agenda.findEvents("Matosinhos", "", "Desporto", mk_Event`Date(7,4,2020));
			assertTrue(eventsFound <>{});
			
		);
		
		private testLoginRegular: () ==> ()
		testLoginRegular() == (
			dcl outcome: bool;
			agenda.loggedInUser := nil;
			-- fail
		  outcome := agenda.login("sofia@gmail.com", "person12345");
		  assertTrue(outcome = false);
			assertTrue(agenda.loggedInUser = nil);
			
			-- success
			outcome := agenda.login("sofia@gmail.com", "sofia12345");
		  assertTrue(outcome = true);
			assertTrue(agenda.loggedInUser <> nil);
			assertTrue(agenda.loggedInUser = user1);
		);
		
		private testBuyTickets: () ==> ()
		testBuyTickets() == (
			--fail
			dcl outcome: bool := agenda.buyTicket(1, 20001);
			assertTrue(outcome = false);
			assertTrue(event1.getSoldTickets() = 0);
			assertTrue(agenda.loggedInUser.getTicketsBought() = 30);
			 
			--success
			outcome := agenda.buyTicket(6, 2);
			assertTrue(outcome = true);
			assertTrue(event6.getSoldTickets() = 2);
			assertTrue(agenda.loggedInUser.getTicketsBought() = 32);
		);
		
		private testProposeEvent: () ==> ()
		testProposeEvent() == (
			agenda.proposeEvent(proposed1);
			agenda.proposeEvent(proposed2);
		);
		
		private testRejectProposedEvent: () ==> ()
		testRejectProposedEvent() == (
			agenda.rejectProposedEvent(proposed2);
		);
		
		private testAcceptProposedEvent: () ==> ()
		testAcceptProposedEvent() == (
			agenda.acceptProposedEvent(proposed1);
		);
		
		public test: () ==> ()
		test() == (
			testCreateAgenda();
			testAddUser();
			testLoginAdmin();
			testAddEvent();
			testFindByCity();
			testFindByDistrict();
			testFindByCategory();
			testFindByDate();
			testFindEvents();
			testLoginRegular();
			testBuyTickets();
			testProposeEvent();
			testLoginAdmin();
			testRejectProposedEvent();
			testAcceptProposedEvent();
		);
end AgendaTest